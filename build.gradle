plugins {
	id 'fabric-loom' version "$loom_version"
	id 'maven-publish'
	id 'java-library'
}

group 'dev.lazurite'
version quadz_version

repositories {
	mavenLocal()
	maven { url 'https://lazurite.dev/releases' }
	maven { url 'https://maven.shedaniel.me' }
	maven { url 'https://maven.modmuss50.me' }
	maven { url 'https://maven.terraformersmc.com/releases' }
	maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
}

dependencies {
	minecraft "com.mojang:minecraft:$minecraft_version"
	mappings loom.officialMojangMappings()

	// Fabric
	modImplementation "net.fabricmc:fabric-loader:$fabric_loader_version"
	modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_api_version"

	// Rayon
	modImplementation "dev.lazurite:rayon-fabric:$rayon_version"

	// Remote
	modImplementation "dev.lazurite:remote:$remote_version"

	// Form
	modImplementation "dev.lazurite:form:$form_version"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}

	withSourcesJar()
}

jar.from 'LICENSE'

tasks.withType ProcessResources, {
	inputs.property 'version', quadz_version

	filesMatching 'fabric.mod.json', {
		expand version: quadz_version
	}
}

publishing {
	repositories {
		mavenLocal()

		maven {
			name 'Lazurite'
			url 'https://lazurite.dev/releases'

			credentials {
				username property('publish.lazurite.alias')
				password property('publish.lazurite.secret')
			}
		}
	}

	publications {
		quadz MavenPublication, {
			from components.java
		}
	}
}