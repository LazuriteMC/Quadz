plugins {
	id 'fabric-loom' version "$loom_version"
	id 'maven-publish'
	id 'java-library'
}

group 'dev.lazurite'
version quadz_version

loom {
	accessWidenerPath = file("src/main/resources/quadz.accessWidener")
}

repositories {
	maven {
		name 'Lazurite'
		url 'https://lazurite.dev/releases'
	}

	maven {
		name 'Gecko'
		url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
	}

	mavenLocal()
}

dependencies {
	minecraft "com.mojang:minecraft:$minecraft_version"
	mappings loom.officialMojangMappings()

	// Fabric
	modImplementation "net.fabricmc:fabric-loader:$fabric_loader_version"
	modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_api_version"

	// Lazurite
//	modApi include("dev.lazurite:lattice:$lattice_version")
	modApi "dev.lazurite:rayon-fabric:$rayon_version"
//	modApi include("dev.lazurite:corduroy:$corduroy_version")
	modApi include("dev.lazurite:form:$form_version")
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}

	withSourcesJar()
}

jar.from 'LICENSE'

tasks.withType ProcessResources, {
	inputs.property 'version', quadz_version

	filesMatching 'fabric.mod.json', {
		expand version: quadz_version
	}
}

publishing {
	if (hasProperty('publish.lazurite.alias') && hasProperty('publish.lazurite.secret')) {
		repositories {
			maven {
				name 'Lazurite'
				url 'https://lazurite.dev/releases'

				credentials {
					username property('publish.lazurite.alias')
					password property('publish.lazurite.secret')
				}
			}
		}
	}

	publications {
		quadz MavenPublication, {
			from components.java
		}
	}
}