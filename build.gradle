plugins {
	id 'fabric-loom' version "$loom_version"
	id 'maven-publish'
}

group 'dev.lazurite'
version quadz_version

repositories {
	maven { url 'https://lazurite.dev/releases' }
	maven { url 'https://maven.shedaniel.me' }
	maven { url 'https://maven.modmuss50.me' }
	maven { url 'https://maven.terraformersmc.com/releases' }
	maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven' }
}

dependencies {
	minecraft "com.mojang:minecraft:$minecraft_version"
	mappings loom.officialMojangMappings()

	// Fabric
	modImplementation "net.fabricmc:fabric-loader:$fabric_loader_version"
	modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_api_version"

	// Gecko Lib 3
	modImplementation "software.bernie.geckolib:$geckolib_version"

	// Rayon
	modImplementation "dev.lazurite:rayon-fabric:$rayon_version"

	// Lattice
	modImplementation "dev.lazurite:lattice:$lattice_version"
	include "dev.lazurite:lattice:$lattice_version"

	// Mod Menu + Cloth
	modImplementation "com.terraformersmc:modmenu:$modmenu_version"
	modImplementation "me.shedaniel.cloth:cloth-config-fabric:$cloth_version", { transitive false }

	// Fiber
	modImplementation "me.zeroeightsix:fiber:$fiber_version"
	include "me.zeroeightsix:fiber:$fiber_version"
}

java {
	toolchain.languageVersion = JavaLanguageVersion.of 17
	withSourcesJar()
}

jar {
	from 'LICENSE'
}

loom {
	accessWidenerPath = file 'src/main/resources/quadz.accesswidener'
}

tasks.withType ProcessResources, {
	inputs.property 'version', quadz_version

	filesMatching 'fabric.mod.json', {
		expand version: quadz_version
	}
}

publishing {
	repositories {
		mavenLocal()

		maven {
			name 'Lazurite'
			url 'https://lazurite.dev/releases'

			credentials {
				username property('publish.lazurite.alias')
				password property('publish.lazurite.secret')
			}
		}
	}

	publications {
		quadz MavenPublication, {
			from components.java
		}
	}

}